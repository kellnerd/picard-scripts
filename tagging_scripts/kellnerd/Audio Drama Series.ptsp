title: Audio Drama Series
author: kellnerd
script_language_version: '1.1'
script: |
  $if($inmulti($lower(%releasetype%),audio drama),

  $noop( Use series tags for audio dramas )
  $copy(series,_releasegroup_series)
  $copy(seriesnumber,_releasegroup_seriesnumber)

  $noop( Expression to detect an episode number with optional prefix and leading zeros )
  $set(_EpisodeNumberRegex,\(?:\(?:\, |^\)\(?:Episode|Folge\)\)? 0*\(\\d+\))

  $noop( Use first set of series tags or try to extract series title and episode number from the release title )
  $noop( Try to extract episode title, fallback to release title )
  $set(_SeriesTitle,$if2($getmulti(%series%,0),$rsearch(%album%,\(.+?\)\(?:%_EpisodeNumberRegex%\)?:)))
  $set(_SeriesEpisode,$if2($getmulti(%seriesnumber%,0),$rsearch(%album%,%_EpisodeNumberRegex%:)))
  $set(_SeriesEpisodeTitle,$if2($rsearch(%album%,.+?: \(.+\)),%album%))

  $noop( Put episode number in release title into parentheses, stripping common prefixes )
  $set(album,$rreplace(%album%,%_EpisodeNumberRegex%:, \(\\1\)))

  )
id: e8560b74-2b3f-467a-bfb4-2ff1dac5dffd
