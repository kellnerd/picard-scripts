$noop( Clear all temporary variables to which values will be appended, otherwise they have duplicates on refresh )
$unset(_PerformanceETI)

$noop( Delete unwanted tags )
$foreach(script; asin,
	$delete(%_loop_value%)
)

$noop( Delete multi-value tags if they are identical to their string counterparts: Kodi looks for *ARTISTS first and falls back to *ARTIST )
$if($lt($lenmulti(%artists%),2),
	$delete(artists)
,$noop( else: Prefer multi-value sort names )
	$setmulti(artistsort,%_artists_sort%)
)
$if($lt($lenmulti(%_albumartists%),2),
	$delete(albumartists)
,
	$setmulti(albumartists,%_albumartists%) $noop( Tag is missing by default )
	$setmulti(albumartistsort,%_albumartists_sort%)
)

$noop( Use original date as primary date, backup date of the specific release )
$set(releasedate,%date%)
$set(_releaseyear,$year(%releasedate%))
$set(date,%originaldate%)

$noop( Capitalize release status and types, use uppercase for EP, drop Other )
$set(releasestatus,$title(%releasestatus%))
$setmulti(releasetype,$map(%releasetype%,$if($eq(%_loop_value%,ep),$upper(%_loop_value%),$if($ne(%_loop_value%,other),$title(%_loop_value%)))))

$noop( Use additional sort names that are provided by my plugin )
$setmulti(lyricistsort,%_lyricistsort%)
$setmulti(writersort,%_writersort%)

$noop( Use all available disambiguation comments, the most specific first )
$set(comment,$join($unique(%_recordingcomment%; %_releasecomment%; %_releasegroupcomment%),\n))

$noop( Use square brackets instead of rounded brackets for ETI )
$set(title,$rreplace(%title%,\\\(\(?=[^\(\)]*\(?:edit|mix|version|take|live|instrumental|dialogue|interview|bonus\)\)\([^\(\)]+\)\\\),[\\1]))

$noop( Use performance attributes, add attributes that are not already part of the title as ETI )
$if(%_performance_attributes%,
	$setmulti(performance_attributes,$title(%_performance_attributes%))
	$foreach(instrumental; live; cover,
		$if($and($inmulti(%_performance_attributes%,%_loop_value%),$not($in(%title%,%_loop_value%))),
			$copymerge(_PerformanceETI,_loop_value)
		)
	)
)
$if(%_PerformanceETI%,
	$set(title,%title% [$join(%_PerformanceETI%, )])
)

$noop( Use country names instead of two letter codes )
$set(releasecountry,$countryname(%releasecountry%))

$noop( Frequently used constants )
$set(_MultiDisc,$gt(%totaldiscs%,1))
