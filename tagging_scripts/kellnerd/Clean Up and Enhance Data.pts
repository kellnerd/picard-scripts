$noop( Delete unwanted tags )
$foreach(script; asin,
	$delete(%_loop_value%)
)

$noop( Delete multi-value tags if they are identical to their string counterparts: Kodi looks for *ARTISTS first and falls back to *ARTIST )
$if($lt($lenmulti(%artists%),2),
	$delete(artists)
,$noop( else: Prefer multi-value sort names )
	$setmulti(artistsort,%_artists_sort%)
)
$if($lt($lenmulti(%_albumartists%),2),
	$delete(albumartists)
,
	$setmulti(albumartists,%_albumartists%) $noop( Tag is missing by default )
	$setmulti(albumartistsort,%_albumartists_sort%)
)

$noop( Use original date as primary date, backup date of the specific release )
$set(releasedate,%date%)
$set(_releaseyear,$left(%releasedate%,4))
$set(date,%originaldate%)

$noop( Use lyricist and composer tag instead of writer )
$setmulti(lyricistsort,%_lyricistsort%) $noop( Tag is missing by default )
$copymerge(lyricist,writer)
$copymerge(lyricistsort,_writersort)
$if($not(%_SpecialReleaseType%),
	$copymerge(composer,writer)
	$copymerge(composersort,_writersort)
)
$unset(writer)

$noop( Use all existing disambiguation comments )
$foreach(_releasegroupcomment; _releasecomment; _recordingcomment,
	$copymerge(comment,%_loop_value%)
)

$noop( Use square brackets instead of rounded brackets for ETI )
$set(title,$rreplace(%title%,\\\(\(?=[^\(\)]*\(?:edit|mix|version|take|live|instrumental|dialogue|interview|bonus\)\)\([^\(\)]+\)\\\),[\\1]))

$noop( Use performance attributes, add attributes that are not already part of the title as ETI )
$if(%_performance_attributes%,
	$setmulti(performance_attributes,%_performance_attributes%)
	$foreach(instrumental; live; cover,
		$if($and($inmulti(%performance_attributes%,%_loop_value%),$not($in(%title%,%_loop_value%))),
			$copymerge(_PerformanceETI,_loop_value)
		)
	)
)
$if(%_PerformanceETI%,
	$set(title,%title% [$join(%_PerformanceETI%, )])
)

$noop( Lyrics are not part of an instrumental recording )
$if($inmulti(%_performance_attributes%,instrumental),
	$delete(lyricist)
	$delete(lyricistsort)
)

$noop( Frequently used constants )
$set(_MultiDisc,$gt(%totaldiscs%,1))
