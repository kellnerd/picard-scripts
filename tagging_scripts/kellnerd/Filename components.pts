$noop( Frequently used constants )
$set(_MultiDisc,$gt(%totaldiscs%,1))
$set(_ReleaseYear,$year($if2(%releasedate%,%date%)))

$noop( Compose version info string for the album )
$if($gt(%_ReleaseYear%,%originalyear%),
	$set(_Reissue,%_ReleaseYear%)
)
$if($or(%_MultiDisc%,$ne_all(%media%,CD,Digital Media)),
	$set(_ReleaseFormat,$if(%_MultiDisc%,%totaldiscs%x)%media%)
)
$setmulti(_AlbumInfo,%_Reissue%; %_ReleaseFormat%)
$cleanmulti(_AlbumInfo)

$noop( General snippets )
$set(_Year,$if2(%originalyear%,%_ReleaseYear%). )
$if(%_SeriesEpisode%,
	$set(_Volume,\(%_SeriesEpisode%\) )
)
$if(%_AdditionalReleaseTypes%,
	$set(_AdditionalReleaseTypes, [$join(%_AdditionalReleaseTypes%,+)])
)
$if(%_AlbumInfo%,
	$set(_AlbumInfo, [$join(%_AlbumInfo%, )])
)

$noop( Standard path components )
$set(_ArtistFolder,$join($replace(%albumartistsort%,/,-), & ))
$set(_AlbumFolder,
	%_Year%
	$replace(%album%,/,-)
	%_AdditionalReleaseTypes%
	%_AlbumInfo%
)
$set(_TrackTitle,$replace(%title%,/,-))

$noop( Special path components for releases that are part of a series )
$if(%_SeriesTitle%,
	$set(_SeriesFolder,$replace(%_SeriesTitle%,/,-))
	$set(_EpisodeFolder,
		%_Year%
		%_Volume%
		$replace(%_SeriesEpisodeTitle%,/,-)
		%_AdditionalReleaseTypes%
		%_AlbumInfo%
	)
)
