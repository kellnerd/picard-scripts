$noop(## Generate path from custom variables, fall back to standard tags)
$set(_path,
	$noop(## Root directory and artist/series folder)
	$if2(%_SpecialReleaseType%,Music)
	/
	$if2(%_SeriesFolder%,%_ArtistFolder%,%albumartist%,%artist%)
	/
	$noop(## Album/Episode folder, except for standalone recordings)
	$if(%albumartist%,
		$if2(%_EpisodeFolder%,%_AlbumFolder%,%album%)
	/)
	$noop(## Disc and track number, if available, except for standalone recordings)
	$if($and(%tracknumber%,%albumartist%),
		$if($gt(%totaldiscs%,1),
			$if2(%_PaddedDiscNum%,%discnumber%)
		-)
		$if2(%_PaddedTrackNum%,%tracknumber%)
	. )
	$noop(## Artist credits, except for special releases)
	$if($not(%_SpecialReleaseType%),
		$if2(%_TrackArtist%,%artist%)
	 - )
	$noop(## Track/Recording title)
	$if2(%_TrackTitle%,%title%)
)

$noop(## Additional custom replacements, e.g. to prevent replacement of illegal characters with an underscore)
$set(_path,$replace(%_path%,: , - ))
$set(_path,$replace(%_path%,???,Fragezeichen))
$set(_path,$replace(%_path%,?,))

$noop(## Output the constructed path, remove line breaks and indentation caused by script formatting)
$rreplace(%_path%,[\\t\\n],)
